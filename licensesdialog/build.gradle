apply plugin: 'com.android.library'
apply plugin: "org.sonarqube"
apply from: rootProject.file('gradle/jacoco.gradle')

project.description = "LicensesDialog (Library)"

android {
    compileSdkVersion rootProject.ext.compileSdk
    buildToolsVersion rootProject.ext.buildTools

    defaultConfig {
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionName project.version
        versionCode buildVersionCode()

        buildConfigField 'String', 'GIT_SHA', "\"${gitScmVersion()}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        lintConfig rootProject.file('lint.xml')
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    // Android
    implementation "androidx.annotation:annotation:${annotationVersion}"
    implementation "androidx.appcompat:appcompat:${appcompatVersion}"
    implementation "androidx.fragment:fragment:${fragmentVersion}"

    // Testing
    testImplementation 'junit:junit:4.13-beta-1'
//	  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testImplementation 'androidx.test:core:1.1.0'
    testImplementation 'org.robolectric:robolectric:4.1'
}

sonarqube {
    properties {
        property "sonar.projectKey", "$project.group"
        property "sonar.projectName", "LicensesDialog"
        property "sonar.exclusions", [].join(',')
        property "sonar.scm.provider", "git"
        property "sonar.android.lint.report", "build/reports/lint-results.xml"
        property "sonar.junit.reportPaths", "build/test-results/testDebugUnitTest"
        property "sonar.jacoco.reportPaths", "build/jacoco/testDebugUnitTest.exec"
    }
}

apply from: rootProject.file('gradle/publishMavenLocal.gradle')