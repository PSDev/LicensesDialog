buildscript {
    ext {
        // Plugin versions
        androidGradlePluginVersion = "4.2.1"
        gradleVersionsPluginVersion = "0.36.0"
        sonarqubeGradlePluginVersion = "3.3"
        // Dependency versions
        androidXAppCompatVersion = "1.2.0"
        androidXWebKitVersion = "1.4.0"
        androidXTestVersion = "1.3.0"
        junitVersion = "4.13.2"
        robolectricVersion = '4.5.1'
    }
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$androidGradlePluginVersion"
        classpath "com.github.ben-manes:gradle-versions-plugin:$gradleVersionsPluginVersion"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:$sonarqubeGradlePluginVersion"
    }
}

apply plugin: 'com.github.ben-manes.versions'

allprojects {
    group "de.psdev.licensesdialog"
    version "2.3.0-SNAPSHOT"

    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    apply from: rootProject.file('gradle/versioning.gradle')

    dependencyUpdates.resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

wrapper {
    gradleVersion = '7.0.2'
    distributionType = "all"
}