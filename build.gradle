buildscript {
    ext {
        // Plugin versions
        androidGradlePluginVersion = '4.1.1'
        gradleVersionsPluginVersion = "0.21.0"
        sonarqubeGradlePluginVersion = "2.7.1"
        // Dependency versions
        appcompatVersion = "1.2.0"
        androidXTestVersion = "1.3.0"
        junitVersion = "4.13.1"
        robolectricVersion = "4.3"
        androidXWebKitVersion = "1.4.0"
    }
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$androidGradlePluginVersion"
        classpath "com.github.ben-manes:gradle-versions-plugin:$gradleVersionsPluginVersion"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:$sonarqubeGradlePluginVersion"
    }
}

apply plugin: 'com.github.ben-manes.versions'

allprojects {
    group "de.psdev.licensesdialog"
    version "2.2.0-SNAPSHOT"

    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    apply from: rootProject.file('gradle/versioning.gradle')

    dependencyUpdates.resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

wrapper {
    gradleVersion = '6.7.1'
    distributionType = "all"
}